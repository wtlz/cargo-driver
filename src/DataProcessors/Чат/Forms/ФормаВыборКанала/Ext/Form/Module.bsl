#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьНачальныеНастройки();
	
	УстановитьТекущееТранспортноеСредство();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Объект.ТекущееТранспортноеСредство) Тогда
		Возврат;
	КонецЕсли;
	
	#Если МобильноеПриложениеКлиент Тогда
	
	ОписаниеОповещенияОВводе = Новый ОписаниеОповещения("ПослеВводаНомераТранспортногоСредства", ЭтаФорма);
	НомерТранспортногоСредства = "";

	КвалификаторыСтроки = Новый КвалификаторыСтроки(9);
    ОписаниеСтроки = Новый ОписаниеТипов("Строка", ,КвалификаторыСтроки);	
	ПоказатьВводЗначения(ОписаниеОповещенияОВводе, НомерТранспортногоСредства, 
		"Введите номер авто", ОписаниеСтроки);
	
	#КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ведущий(Команда)

	ОткрытьФормуКанала(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Экспедитор(Команда)

	ОткрытьФормуКанала(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура Механик(Команда)
	
	ОткрытьФормуКанала(Команда.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьТекущееТранспортноеСредство()
	
	Объект.ТекущееТранспортноеСредство = Константы.ТекущееТранспортноеСредство.Получить();
	Заголовок = Объект.ТекущееТранспортноеСредство;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВводаНомераТранспортногоСредства(Результат, ДопПараметры) Экспорт

	ПослеВводаНомераНаСервере(СокрЛП(Результат));

	Если НЕ ЗначениеЗаполнено(Объект.ТекущееТранспортноеСредство) Тогда
	    Сообщить("Работа приложения без номера транспорта невозможна.", СтатусСообщения.Важное);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ТекущееТранспортноеСредство) Тогда
		ЗавершитьРаботуСистемы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеВводаНомераНаСервере(Результат)

	URL = Константы.АдресСервера.Получить() + "/GetVehicle";

	Данные = Новый Структура("Код", Результат);
	
	Ответ = КоннекторHTTP.Get(URL, Данные);	
	
	Если Ответ.КодСостояния <> 200 Тогда
		Сообщить("Ошибка установки номера: " + Ответ.КодСостояния);
		Возврат;
	КонецЕсли;
	
	РезультатОтвета = КоннекторHTTP.КакJson(Ответ);
	
	Ссылка = Справочники.ТранспортныеСредства.СоздатьТранспортноеСредство(РезультатОтвета);
	
	Константы.ТекущееТранспортноеСредство.Установить(Ссылка);
	
	УстановитьТекущееТранспортноеСредство();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКанала(ИмяКанала)

	ПараметрыФормы = Новый Структура("ИмяКанала", ИмяКанала);
	ПараметрыФормы.Вставить("ТранспортноеСредство", Объект.ТекущееТранспортноеСредство);
	
	ОткрытьФорму("Обработка.Чат.Форма.ФормаКанала", ПараметрыФормы, ЭтаФорма, ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьНачальныеНастройки()
	
	Если ПустаяСтрока(Константы.АдресСервера.Получить()) Тогда
		Константы.АдресСервера.Установить("http://45.8.230.101/cargo-driver/hs/exchange/v1");	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
