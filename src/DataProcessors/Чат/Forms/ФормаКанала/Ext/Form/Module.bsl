#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТранспортноеСредство", Объект.ТекущееТранспортноеСредство);
	Объект.ТекущийКанал = ПолучитьРольСотрудника(Параметры.ИмяКанала); 
	Объект.ТекущийВодитель = ПолучитьТекущегоСотрудникаПоРоли(Объект.ТекущееТранспортноеСредство, ПолучитьРольСотрудника("Водитель"));
	
	УстановитьЭлементОтбора("КаналЧата", Объект.ТекущийКанал);
	
	УстановитьЭлементОтбора("ТранспортноеСредство", Объект.ТекущееТранспортноеСредство);
	
	Заголовок = "Чат: " + Параметры.ИмяКанала;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокСообщений

&НаКлиенте
Процедура СписокСообщенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьСообщение(Команда)
	
	Если ПустаяСтрока(Объект.ТекстНовогоСообщения) Тогда
		Возврат;
	КонецЕсли;
	
	КлючЗаписи = ОтправитьСообщениеНаСервере();
	
	Элементы.СписокСообщений.Обновить();
	Элементы.СписокСообщений.ТекущаяСтрока = КлючЗаписи;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНовыеСообщения(Команда)
	
	ПроверитьНовыеСообщенияНаСервере(Элементы.СписокСообщений.ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЭлементОтбора(ПолеОтбора, ЗначениеОтбора)
	
	ЭлементОтбора = СписокСообщений.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПолеОтбора);
	ЭлементОтбора.ПравоеЗначение = ЗначениеОтбора;

КонецПроцедуры

&НаСервере
Функция ОтправитьСообщениеНаСервере()

	 Сообщение = Справочники.СообщенияЧата.СоздатьЭлемент();
	 Сообщение.ТекстСообщения = Объект.ТекстНовогоСообщения;
	 Сообщение.Автор = Объект.ТекущийВодитель;
	 Сообщение.ТранспортноеСредство = Объект.ТекущееТранспортноеСредство;
	 Сообщение.КаналЧата = Объект.ТекущийКанал;
	 
	 #Если НЕ МобильноеПриложениеСервер Тогда
		 Сообщение.Автор = ПараметрыСеанса.ТекущийПользователь;
	 #КонецЕсли
	 
	 Сообщение.Записать();
	 
	 РегистрыСведений.СтатусыСообщений.СоздатьЗаписьСтатусСообщения(Сообщение.Ссылка, Перечисления.СтатусыСообщений.НеОтправлено);
	 
	 #Если МобильноеПриложениеСервер Тогда
		 ОтправитьСозданноеСообщение(Сообщение);
	 #Иначе
		 РегистрыСведений.СтатусыСообщений.СоздатьЗаписьСтатусСообщения(Сообщение.Ссылка, Перечисления.СтатусыСообщений.Отправлено);
	 #КонецЕсли
	 
	 Объект.ТекстНовогоСообщения = "";
	 
	 Возврат Сообщение.Ссылка;
	 
КонецФункции

&НаСервере
Процедура ОтправитьСозданноеСообщение(Сообщение)

	URL = Константы.АдресСервера.Получить() + "/CreateMessage";
	
	Данные = Новый Структура("ТекстСообщения, ДатаСоздания, КаналЧата");
	ЗаполнитьЗначенияСвойств(Данные, Сообщение);
	
	Данные.Вставить("Ссылка", Сообщение.Ссылка.УникальныйИдентификатор());
	Данные.Вставить("Автор", Сообщение.Автор.УникальныйИдентификатор());
	Данные.Вставить("ТранспортноеСредство", Сообщение.ТранспортноеСредство.УникальныйИдентификатор());
	
	Ответ = КоннекторHTTP.Put(URL, Данные);	
	
	Если Ответ.КодСостояния <> 200 Тогда
		Сообщить("Ошибка отправки сообщения: " + Ответ.КодСостояния);
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.СтатусыСообщений.СоздатьЗаписьСтатусСообщения(Сообщение.Ссылка, Перечисления.СтатусыСообщений.Отправлено);

КонецПроцедуры

&НаСервере
Функция ПолучитьТекущегоСотрудникаПоРоли(ТранспортноеСредство, Роль)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗакрепленныеСотрудникиСрезПоследних.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.ЗакрепленныеСотрудники.СрезПоследних(
		|			,
		|			ТранспортноеСредство = &ТранспортноеСредство
		|				И Сотрудник.РольСотрудника = &РольСотрудника) КАК ЗакрепленныеСотрудникиСрезПоследних";
	
	Запрос.УстановитьПараметр("РольСотрудника", Роль);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Сотрудник;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПолучитьРольСотрудника(ИмяРоли)
	
	Возврат Перечисления.РолиСотрудников[ИмяРоли];
	
КонецФункции

&НаСервере
Процедура ПроверитьНовыеСообщенияНаСервере(ТекущаяСтрока)

	Перем ПоследнееСообщение;
	
	URL = Константы.АдресСервера.Получить() + "/GetNewMessages";
	
	Данные = Новый Структура;
	
	Данные.Вставить("ТранспортноеСредство", Константы.ТекущееТранспортноеСредство.Получить().УникальныйИдентификатор());
	
	Ответ = КоннекторHTTP.Get(URL, Данные);	
	
	Если Ответ.КодСостояния <> 200 Тогда
		Сообщить("Код ошибки отправки сообщения: " + Ответ.КодСостояния); // TODO Переписать Сообщить на Сообщение пользователю из общего модуля
		Возврат;
	КонецЕсли;
	
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	АвторыСообщений = Новый Соответствие;
	
	Для каждого Сообщение Из Результат Цикл
		
		ПараметрыСообщения = ОбщегоНазначения.СоответствиеВСтруктуру(Сообщение);
		ПоследнееСообщение = Справочники.СообщенияЧата.СоздатьСообщение(ПараметрыСообщения);
		
		АвторыСообщений.Вставить(ПараметрыСообщения.Автор, ПоследнееСообщение.Автор);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПоследнееСообщение) Тогда
		ТекущаяСтрока = ПоследнееСообщение;
	КонецЕсли;
	
	ПолучитьНовыхСотрудников(АвторыСообщений);
	
КонецПроцедуры

Процедура ПолучитьНовыхСотрудников(АвторыСообщений)

	СотрудникиБезСсылки = ПолучитьСотрудниковБезСсылки(АвторыСообщений);

	Если НЕ СотрудникиБезСсылки.Количество() Тогда
		Возврат;	
	КонецЕсли;	
	
	URL = Константы.АдресСервера.Получить() + "/GetEmployees";
	
	Данные = Новый Структура;
	
	Данные.Вставить("Сотрудники", СтрСоединить(СотрудникиБезСсылки, ";"));
	
	Ответ = КоннекторHTTP.Get(URL, Данные);	
	
	Если Ответ.КодСостояния <> 200 Тогда
		Сообщить("Код ошибки получения данных сотрудников: " + Ответ.КодСостояния);
		Возврат;
	КонецЕсли;
	
	Результат = КоннекторHTTP.КакJson(Ответ);
	
	Для каждого Соответствие Из Результат Цикл
		СтруктураДанных = ОбщегоНазначения.СоответствиеВСтруктуру(Соответствие);
		Справочники.Сотрудники.СоздатьСотрудника(СтруктураДанных);	
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьСотрудниковБезСсылки(Знач АвторыСообщений)
	
	Сотрудники = Новый Массив;
	
	Для каждого КлючИЗначение Из АвторыСообщений Цикл
		
		Если ЗначениеЗаполнено(КлючИЗначение.Значение.ВерсияДанных) Тогда
			Продолжить;	
		КонецЕсли;	
		
		Сотрудники.Добавить(КлючИЗначение.Ключ);
		
	КонецЦикла;

	Возврат Сотрудники;
	
КонецФункции

#КонецОбласти