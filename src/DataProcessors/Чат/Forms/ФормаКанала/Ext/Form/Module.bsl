
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.ТекущееТранспортноеСредство = Константы.ТекущееТранспортноеСредство.Получить();
	Объект.ТекущийКанал = ПолучитьРольСотрудника(Параметры.ИмяКанала); 
	Объект.ТекущийВодитель = ПолучитьТекущегоСотрудникаПоРоли(Объект.ТекущееТранспортноеСредство, ПолучитьРольСотрудника("Водитель"));
	
	УстановитьЭлементОтбора("КаналЧата", Объект.ТекущийКанал);
	
	УстановитьЭлементОтбора("ТранспортноеСредство", Объект.ТекущееТранспортноеСредство);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЭлементОтбора(ПолеОтбора, ЗначениеОтбора)
	
	ЭлементОтбора = СписокСообщений.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПолеОтбора);
	ЭлементОтбора.ПравоеЗначение = ЗначениеОтбора;

КонецПроцедуры

&НаСервере
Функция ОтправитьСообщениеНаСервере()

	 Сообщение = Справочники.СообщенияЧата.СоздатьЭлемент();
	 Сообщение.ТекстСообщения = Объект.ТекстНовогоСообщения;
	 Сообщение.Автор = Объект.ТекущийВодитель;
	 Сообщение.Записать();
	 
	 МенеджерЗаписи = РегистрыСведений.СтатусыСообщений.СоздатьМенеджерЗаписи();
	 МенеджерЗаписи.Период = ТекущаяДата();
	 МенеджерЗаписи.Сообщение = Сообщение.Ссылка;
	 МенеджерЗаписи.Статус = Перечисления.СтатусыСообщений.НеОтправлено;
	 МенеджерЗаписи.Записать();
	 
	 МенеджерЗаписи = РегистрыСведений.ИсторияЧата.СоздатьМенеджерЗаписи();
	 МенеджерЗаписи.Сообщение = Сообщение.Ссылка;
	 МенеджерЗаписи.ТранспортноеСредство = Объект.ТекущееТранспортноеСредство;
	 МенеджерЗаписи.КаналЧата = Объект.ТекущийКанал;
	 МенеджерЗаписи.Записать();
	 
	 Объект.ТекстНовогоСообщения = "";
	 
	 СтруктураКлюча = Новый Структура("ТранспортноеСредство, Сообщение, КаналЧата");
	 СтруктураКлюча.Вставить("Период");
	 ЗаполнитьЗначенияСвойств(СтруктураКлюча, МенеджерЗаписи);
	 КлючЗаписи = РегистрыСведений.ИсторияЧата.СоздатьКлючЗаписи(СтруктураКлюча);
	 
	 Возврат КлючЗаписи;
	 
КонецФункции

&НаКлиенте
Процедура ОтправитьСообщение(Команда)
	
	Если ПустаяСтрока(Объект.ТекстНовогоСообщения) Тогда
		Возврат;
	КонецЕсли;
	
	КлючЗаписи = ОтправитьСообщениеНаСервере();
	
	Элементы.СписокСообщений.Обновить();
	Элементы.СписокСообщений.ТекущаяСтрока = КлючЗаписи;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокСообщенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекущегоСотрудникаПоРоли(ТранспортноеСредство, Роль)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗакрепленныеСотрудникиСрезПоследних.Сотрудник КАК Сотрудник
		|ИЗ
		|	РегистрСведений.ЗакрепленныеСотрудники.СрезПоследних(
		|			,
		|			ТранспортноеСредство = &ТранспортноеСредство
		|				И Сотрудник.РольСотрудника = &РольСотрудника) КАК ЗакрепленныеСотрудникиСрезПоследних";
	
	Запрос.УстановитьПараметр("РольСотрудника", Роль);
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Сотрудник;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьРольСотрудника(ИмяРоли)
	
	// TO_DO убрать поиск по наименованию
	Возврат Справочники.РолиСотрудников.НайтиПоНаименованию(ИмяРоли);
	
КонецФункции
