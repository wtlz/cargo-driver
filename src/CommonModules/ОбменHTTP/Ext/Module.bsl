
Функция ОбработкаЗапроса(Запрос, ЭтоPOST) Экспорт
	
	Перем Ответ, Причина, ТелоОтвета;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыЗапроса = ПолучитьПараметрыЗапроса(Запрос, ЭтоPOST);
	
	ИмяМетода = Запрос.ПараметрыURL.Получить("ИмяМетода");
	
	Если ИмяМетода = "CreateMessage" Тогда
		
		КодОтвета = 200;
		Справочники.СообщенияЧата.СоздатьСообщение(ПараметрыЗапроса);	
		
	ИначеЕсли ИмяМетода = "UpdateSubscriberId" Тогда
		
		КодОтвета = 200;
		Справочники.ТранспортныеСредства.ОбновитьИдентификаторУстройства(ПараметрыЗапроса);	
		
	ИначеЕсли ИмяМетода = "GetNewMessages" Тогда
		
		КодОтвета = 200;
		МассивНовыхСообщений = Справочники.СообщенияЧата.ПолучитьНовыеСообщения(ПараметрыЗапроса);	
		ТелоОтвета = КоннекторHTTP.ОбъектВJson(МассивНовыхСообщений);
		
	ИначеЕсли ИмяМетода = "GetVehicle" Тогда
		
		ТранспортноеСредство = Справочники.ТранспортныеСредства.ПолучитьТранспортноеСредство(ПараметрыЗапроса);	
		Если ЗначениеЗаполнено(ТранспортноеСредство) Тогда
			КодОтвета = 200;
			ТелоОтвета = КоннекторHTTP.ОбъектВJson(ТранспортноеСредство);
		Иначе
			КодОтвета = 404;
			Причина = "Транспортное средство не найдено но номеру";
		КонецЕсли;
		
	ИначеЕсли ИмяМетода = "GetEmployees" Тогда
		
		ДанныеСотрудников = Справочники.Сотрудники.ПолучитьДанныеСотрудников(ПараметрыЗапроса);
		КодОтвета = 200;
		ТелоОтвета = КоннекторHTTP.ОбъектВJson(ДанныеСотрудников);
		
	Иначе
		КодОтвета = 400;
		Причина = "Не найдено имя метода";
	КонецЕсли;
	
	#Если НЕ МобильноеПриложениеСервер Тогда
		Ответ = Новый HTTPСервисОтвет(КодОтвета);
		Ответ.Причина = Причина;
		Если ТелоОтвета <> Неопределено Тогда
			Ответ.УстановитьТелоИзСтроки(ТелоОтвета);
		КонецЕсли;
	#КонецЕсли	
		
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьПараметрыЗапроса(Знач Запрос, Знач ЭтоPOST)
	
	Перем МассивПараметров, Параметр, ПараметрыЗапроса;
	
	ПараметрыЗапроса = Новый Структура;
	
	Если ЭтоPOST Тогда		
		СтрокаЗапроса = СтрЗаменить(Запрос.ПолучитьТелоКакСтроку(), "+", " ");
		ПараметрыЗапроса = РаскодироватьСтроку(СтрокаЗапроса , СпособКодированияСтроки.КодировкаURL);
		МассивПараметров = СтрРазделить(ПараметрыЗапроса, "&", Истина);
		ПараметрыЗапроса = МассивПараметровHTTPЗапросаВСтруктуру(МассивПараметров);
	Иначе
		Для каждого Параметр Из Запрос.ПараметрыЗапроса Цикл
			ПараметрыЗапроса.Вставить(Параметр.Ключ, Параметр.Значение);	
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;

КонецФункции

Функция МассивПараметровHTTPЗапросаВСтруктуру(МассивПараметров) Экспорт
	
	ПараметрыЗапроса = Новый Структура;
	
	Для каждого ПараметрЭлемент Из МассивПараметров Цикл	
		
		Если Не ЗначениеЗаполнено(ПараметрЭлемент) Тогда
			Продолжить;
		КонецЕсли;
		
		НомерСимволаРазделителя = СтрНайти(ПараметрЭлемент, "=");
		ПараметрИмя = Лев(ПараметрЭлемент, НомерСимволаРазделителя - 1);
		Значение = Сред(ПараметрЭлемент, НомерСимволаРазделителя + 1);
		ПараметрыЗапроса.Вставить(ПараметрИмя, Значение);		
		
	КонецЦикла;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции