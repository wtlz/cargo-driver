
Функция ОбработкаЗапроса(Запрос, ЭтоPOST) Экспорт
	
	Перем Ответ, Причина;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыЗапроса = ПолучитьПараметрыЗапроса(Запрос, ЭтоPOST);
	
	ИмяМетода = Запрос.ПараметрыURL.Получить("ИмяМетода");
	
	Если ИмяМетода = "СоздатьСообщение" Тогда
		КодОтвета = 200;
		СоздатьСообщение(ПараметрыЗапроса);	
	Иначе
		КодОтвета = 400;
		Причина = "Не найдено имя метода";
	КонецЕсли;
	
	#Если НЕ МобильноеПриложениеСервер Тогда
		Ответ = Новый HTTPСервисОтвет(КодОтвета);
		Ответ.Причина = Причина;
	#КонецЕсли	
		
	Возврат Ответ;
	
КонецФункции

Функция ПолучитьПараметрыЗапроса(Знач Запрос, Знач ЭтоPOST)
	
	Перем МассивПараметров, Параметр, ПараметрыЗапроса;
	
	ПараметрыЗапроса = Новый Структура;
	
	Если ЭтоPOST тогда		
		ПараметрыЗапроса = РаскодироватьСтроку(СтрЗаменить(Запрос.ПолучитьТелоКакСтроку(),"+"," "),СпособКодированияСтроки.КодировкаURL);
		МассивПараметров = СтрРазделить(ПараметрыЗапроса, "&", Истина);
		ПараметрыЗапроса = МассивПараметровHTTPЗапросаВСтруктуру(МассивПараметров);
	Иначе
		Для каждого Параметр из Запрос.ПараметрыЗапроса цикл
			ПараметрыЗапроса.Вставить(Параметр.Ключ,Параметр.Значение);	
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;

КонецФункции

Функция МассивПараметровHTTPЗапросаВСтруктуру(МассивПараметров) Экспорт
	
	ПараметрыЗапроса = Новый Структура;
	
	Для каждого ПараметрЭлемент из МассивПараметров цикл	
		
		Если Не ЗначениеЗаполнено(ПараметрЭлемент) тогда
			Продолжить;
		КонецЕсли;
		
		НомерСимволаРазделителя = СтрНайти(ПараметрЭлемент, "=");
		ПараметрИмя = Лев(ПараметрЭлемент, НомерСимволаРазделителя - 1);
		Значение = Сред(ПараметрЭлемент, НомерСимволаРазделителя + 1);
		ПараметрыЗапроса.Вставить(ПараметрИмя, Значение);		
		
	КонецЦикла;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

Процедура СоздатьСообщение(ПараметрыЗапроса)

	Ссылка = ПолучитьСсылкуПоУИД("СообщенияЧата", ПараметрыЗапроса.Ссылка);

	Если НЕ ЗначениеЗаполнено(Ссылка.ВерсияДанных) Тогда
	
		СправочникОбъект = Справочники.СообщенияЧата.СоздатьЭлемент();
		СправочникОбъект.УстановитьСсылкуНового(Ссылка);
		
		ЗаполнитьЗначенияСвойств(СправочникОбъект, ПараметрыЗапроса);
		
		СправочникОбъект.Автор = ПолучитьСсылкуПоУИД("Сотрудники", ПараметрыЗапроса.Автор);
		
		СправочникОбъект.ТранспортноеСредство = 
			ПолучитьСсылкуПоУИД("ТранспортныеСредства", ПараметрыЗапроса.ТранспортноеСредство);
			
		СправочникОбъект.КаналЧата = Перечисления.РолиСотрудников[ПараметрыЗапроса.КаналЧата];
		СправочникОбъект.Наименование = ПараметрыЗапроса.ТекстСообщения;
			
		СправочникОбъект.ОбменДанными.Загрузка = Истина;
		СправочникОбъект.Записать();
			
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьСсылкуПоУИД(ИмяСправочника, Ссылка)
	
	УникальныйИдентификатор = Новый УникальныйИдентификатор(Ссылка);
	Возврат Справочники[ИмяСправочника].ПолучитьСсылку(УникальныйИдентификатор);
	
КонецФункции

